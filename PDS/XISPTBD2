 /*----------------------------------------------------------------rexx
   XISPTBL table subroutine demo 2
   Call external routine (actually self) to handle primary- and
   line commands.
 --------------------------------------------------------------------*/

 /* initiaization */
 parse source . . $me .
 $pid  = right(left(space(translate(time('l'),' ','.:'),0),10),7)
 $table= 'T'$pid
 Address ispexec
 "control errors return"

 /* determine if called as command handler */
 arg p1,pid,pcmd,ptable,ptext
 if p1 pcmd='XISPTBL CMD' then Exit PCmdX()
 if p1 pcmd='XISPTBL SEL' then Exit LCmdX()

 /* Mainline */
 "tbend" $table
 "tbcreate" $table "names(zsel data) nowrite share"
 if rc<>0 then exit xmsg('table create rc' rc,8)

 /* populate table */
 zz=outtrap('lst.')
 Address TSO "listalc"
 zz=outtrap('off')
 zsel=''
 t.=''                       /* drop non-unique */
 do n=1 to lst.0
   data=strip(lst.n)
   if t.data<>'',
   |  data='NULLFILE' | data='TERMFILE',
   |  right(data,1)='?' then iterate
   "tbadd" $table
   if rc>0 then call xreturn 'tbadd failed' rc zerrlm
   t.data='x'
 end
 drop t.

 /* call driver, list panel only */
 cc=XISPTBL('table('$table') panel(xisptbll) width(80)',
           'proc('$me') id('$pid') sel(s e v) hdr(Table driver demo 2)',
           'sort(=) icmd(sort) isel(all) dpanel(xisptbld)')

 /* cleanup and terminate */
 Call XReturn

XReturn:
 address ispexec "tbend" $table
 Exit xmsg(arg(1),arg(2))
XMsg: if arg(1)<>'' then say arg(1);return word(arg(2) 0,1)
ISPMSG:
 parse arg zedlmsg
 address ispexec"setmsg msg(isrz000)"
 return 0
XMsg: if arg(1)<>'' then say arg(1);return word(arg(2) 0,1)

 /*- Line command handlers -*/
LCmdX:
 "tbget" ptable
 err=''
 dsn=strip(data)
 Select
   when zsel='E' then "Edit dataset('"dsn"')"
   when zsel='V' then "View dataset('"dsn"')"
   when zsel='S' then do
     cc=listdsi("'"dsn"'")
     call ispmsg dsn sysvolume sysdsorg syslrecl sysblksize
   end
   otherwise err='Y'
 End
 if err='Y' then zsel='?'zsel
 else zsel=''
 "tbput" ptable
 exit 0

 /*- Primary command handlers -*/
PCmdX:
 call ispmsg 'Pcmd' ptext 'not implemeted yet'
 exit 0
